DESCRIPTION = "Tiny Android system to leverage android hardware drivers through libhybris"
LICENSE = "Apache-2.0 & Proprietary"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/Apache-2.0;md5=89aea4e17d99a7cacdbeed46a0096b10"

# don't run package_do_shlibs on our packages as this will add a RDEPENDS from ${PN} to
# ${PN}-dbg
EXCLUDE_FROM_SHLIBS = "1"

PACKAGE_ARCH = "${MACHINE_ARCH}"

RPROVIDES_${PN} += "virtual/android-system-image"

INC_PR = "r5"

do_install() {
    cp -rpv ${WORKDIR}/system ${D}

    ln -sf /system/vendor ${D}/vendor

    install -d ${D}/system/symbols
    if [ -d ${WORKDIR}/symbols ] ; then
        cp -rv ${WORKDIR}/symbols ${D}/system
    fi

    install -d ${D}${sysconfdir}
    ln -sf /system/etc/wifi ${D}${sysconfdir}/wifi
    ln -sf /system/etc/media_codecs.xml ${D}${sysconfdir}/media_codecs.xml
    ln -sf /system/etc/media_profiles ${D}${sysconfdir}/media_profiles.xml
    # add a link for firmware, hardcoded in bluez5's hciattach for bcm43xx
    ln -sf /system/vendor/firmware ${D}${sysconfdir}/firmware

    # Copy android ramdisk so we can use it
    install -d ${D}${libdir}/android-system
    # Support packages with both ramdisk names
    if [ -e ${WORKDIR}/android-ramdisk.img ] ; then
        install -m 0644 ${WORKDIR}/android-ramdisk.img ${D}${libdir}/android-system/ramdisk.img
    elif [ -e ${WORKDIR}/ramdisk.img ] ; then
        install -m 0644 ${WORKDIR}/ramdisk.img ${D}${libdir}/android-system/ramdisk.img
    else
        bbfatal "Ramdisk image is missing but required!!!"
    fi

    if [ -e ${WORKDIR}/filesystem_config.txt ] ; then
        # Fixup permissions of the android binaries in /system
        while read line
        do
            file=`echo $line | cut -d' ' -f 1`
            user=`echo $line | cut -d' ' -f 2`
            group=`echo $line | cut -d' ' -f 3`
            mode=`echo $line | cut -d' ' -f 4`
            chown -h $user:$group ${D}/$file
            # Avoid changing symlinks
            if [ ! -h ${D}/$file ] ; then
                chmod $mode ${D}/$file
            fi
        done < "${WORKDIR}/filesystem_config.txt"
    else
        # do a minimal fixup anyway
        chown -Rh 0:0 ${D}/system
        bbwarn "image doesn't contain a filesystem_config.txt; may cause runtime errors with android system"
    fi
}

# 400+ elf binaries without GNU_HASH and we cannot fix it
INSANE_SKIP_${PN} = "ldflags"
INSANE_SKIP_${PN}-dbg = "ldflags"
# Already stripped and we cannot fix it, this will hide a lot of warnings about files already stripped
INHIBIT_PACKAGE_STRIP = "1"
PACKAGES = "${PN}-dbg ${PN}"
FILES_${PN}-dbg = "/system/symbols"
FILES_${PN} = "/system /vendor /android /default.prop ${libdir}/android-system/ramdisk.img ${sysconfdir}"
